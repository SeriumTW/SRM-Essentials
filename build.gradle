plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta6'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
}

import org.gradle.internal.os.OperatingSystem

ext {
    if (project.hasProperty('hytale_home')) {
        hytaleHome = project.findProperty('hytale_home')
    }
    else {
        def os = OperatingSystem.current()
        if (os.isWindows()) {
            hytaleHome = "${System.getProperty("user.home")}/AppData/Roaming/Hytale"
        }
        else if (os.isMacOsX()) {
            hytaleHome = "${System.getProperty("user.home")}/Library/Application Support/Hytale"
        }
        else if (os.isLinux()) {
            hytaleHome = "${System.getProperty("user.home")}/.var/app/com.hypixel.HytaleLauncher/data/Hytale"
            if (!file(hytaleHome).exists()) {
                hytaleHome = "${System.getProperty("user.home")}/.local/share/Hytale"
            }
        }
    }
}

// Warn if Hytale is not installed
if (!file(hytaleHome).exists()) {
    println "WARNING: Hytale not found at ${hytaleHome}"
    println "Please install Hytale or set hytale_home in gradle.properties"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

// Find the latest build directory in the Hytale installation
def findLatestBuild() {
    def gameDir = file("${hytaleHome}/install/${patchline}/package/game")
    if (gameDir.exists()) {
        def latestFile = new File(gameDir, "latest")
        // Check if 'latest' is a symlink/junction directory (common on Windows)
        if (latestFile.exists() && latestFile.isDirectory()) {
            return "latest"
        }
        // Check if 'latest' is a file containing the build name
        if (latestFile.exists() && latestFile.isFile()) {
            return latestFile.text.trim()
        }
        // Fallback: find highest build-N directory
        def builds = gameDir.listFiles()?.findAll { it.name.startsWith("build-") }?.sort { a, b ->
            def numA = a.name.replace("build-", "").toInteger()
            def numB = b.name.replace("build-", "").toInteger()
            numB <=> numA
        }
        if (builds && !builds.isEmpty()) {
            return builds.first().name
        }
    }
    return "build-6" // Default fallback
}

ext.latestBuild = findLatestBuild()
ext.serverJarPath = "${hytaleHome}/install/${patchline}/package/game/${latestBuild}/Server/HytaleServer.jar"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Hytale Server API (provided by server at runtime)
    compileOnly(files("libs/HytaleServer.jar"))
    // Add server JAR to runtime classpath for running the server
    runtimeOnly(files(serverJarPath))
    
    // Dependencies to bundle with Shadow
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.tomlj:tomlj:1.1.1'
    implementation 'org.jetbrains:annotations:24.1.0'

    // Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Updates the manifest.json file with the latest properties defined in the
// gradle.properties file.
tasks.register('updatePluginManifest') {
    def manifestFile = file('src/main/resources/manifest.json')
    doLast {
        if (!manifestFile.exists()) {
            throw new GradleException("Could not find manifest.json at ${manifestFile.path}!")
        }
        def manifestJson = new groovy.json.JsonSlurper().parseText(manifestFile.text)
        manifestJson.Version = version
        manifestJson.IncludesAssetPack = includes_pack.toBoolean()
        manifestFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(manifestJson))
    }
}

// Makes sure the plugin manifest is up to date.
tasks.named('processResources') {
    dependsOn 'updatePluginManifest'
}

// Configure ShadowJar
shadowJar {
    archiveBaseName.set('SRM-Essentials')
    archiveClassifier.set('')
    destinationDirectory.set(layout.buildDirectory.dir("libs"))

    // Relocate dependencies to avoid conflicts with other plugins
    relocate 'com.google.gson', 'io.github.seriumtw.libs.gson'
    relocate 'org.tomlj', 'io.github.seriumtw.libs.tomlj'
    relocate 'org.antlr', 'io.github.seriumtw.libs.antlr'

    // Merge service files properly
    mergeServiceFiles()
}

// Configure tests
test {
    useJUnitPlatform()
}

// TODO: Re-enable shadowJar when Shadow plugin supports Java 25
// For now, use regular jar
// build {
//     dependsOn shadowJar
// }

def createServerRunArguments(String srcDir) {
    def programParameters = '--allow-op --disable-sentry --assets="' + "${hytaleHome}/install/$patchline/package/game/${latestBuild}/Assets.zip" + '"'
    def modPaths = []
    if (includes_pack.toBoolean()) {
        modPaths << srcDir
    }
    if (load_user_mods.toBoolean()) {
        modPaths << "${hytaleHome}/UserData/Mods"
    }
    if (!modPaths.isEmpty()) {
        programParameters += ' --mods="' + modPaths.join(',') + '"'
    }
    return programParameters
}

// Creates a run configuration in IDEA that will run the Hytale server with
// your plugin and the default assets.
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = createServerRunArguments(sourceSets.main.java.srcDirs.first().parentFile.absolutePath)
        workingDirectory = serverRunDir.absolutePath
    }
}

// Task to run the Hytale server directly
tasks.register('runServer', JavaExec) {
    group = 'application'
    description = 'Runs the Hytale server with the plugin'
    mainClass = 'com.hypixel.hytale.Main'
    classpath = files(serverJarPath) + sourceSets.main.runtimeClasspath
    workingDir = serverRunDir
    jvmArgs = ['--enable-native-access=ALL-UNNAMED']
    args = createServerRunArguments(sourceSets.main.java.srcDirs.first().parentFile.absolutePath).split(' ').toList()
}

// Creates a launch.json file for VSCode with the same configuration
tasks.register('generateVSCodeLaunch') {
    def vscodeDir = file("$projectDir/.vscode")
    def launchFile = file("$vscodeDir/launch.json")
    doLast {
        if (!vscodeDir.exists()) {
            vscodeDir.mkdirs()
        }
        def programParams = createServerRunArguments("\${workspaceFolder}")
        def launchConfig = [
            version: "0.2.0",
            configurations: [
                [
                    type: "java",
                    name: "HytaleServer",
                    request: "launch",
                    mainClass: "com.hypixel.hytale.Main",
                    args: programParams,
                    cwd: "\${workspaceFolder}/run"
                ]
            ]
        ]
        launchFile.text = groovy.json.JsonOutput.prettyPrint(groovy.json.JsonOutput.toJson(launchConfig))
    }
}
